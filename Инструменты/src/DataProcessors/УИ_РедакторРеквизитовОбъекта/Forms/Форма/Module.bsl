
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Объект") Тогда
		ПрочитатьДанныеОбъекта();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения,
		СтандартнаяОбработка)
		//TODO: Вставить содержимое обработчика
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыбранныйОбъектПриИзменении(Элемент)
	ПрочитатьДанныеОбъекта();
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ТабличнаяЧастьПриИзменении(Элемент)
//Нужно пересчитать номера строк табличной части
	Номер = 1;
	Для Каждого СтрокаТЧ ИЗ ЭтотОбъект[Элемент.Имя] Цикл
		СтрокаТЧ.НомерСтроки = Номер;

		Номер = Номер + 1;
	КонецЦикла;

КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ТабличнаяЧастьПриНачалеРедактирования(Элемент,
		НоваяСтрока, Копирование)
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если НоваяСтрока Тогда
		ТекДанные.НомерСтроки = ЭтаФорма[Элемент.Имя].Количество();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перечитать(Команда)
	ЗаполнитьДанныеОбъектаНаФорме();
	Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДанныхОбъекта

&НаСервере
Функция ТекущиеРеквизитыФормы(ПутьКДанным = "")
	РеквизитыФормы = ПолучитьРеквизиты(ПутьКДанным);
	СоответствиеТекущихРеквизитов = Новый Соответствие;

	Для Каждого ТекР ИЗ РеквизитыФормы Цикл
		СоответствиеТекущихРеквизитов.Вставить(ТекР.Имя, ТекР);
	КонецЦикла;

	Возврат СоответствиеТекущихРеквизитов;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МассивПрефиксовДобавляемыхРеквизитовФормы()
	Массив = Новый Массив;
	Массив.Добавить("СтандартныйРеквизит_");
	Массив.Добавить("Реквизит_");

	Возврат Массив;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДобавляемыйРеквизит(ИмяРеквизита)
	ПрефиксыДобавляемыхРеквизитов = МассивПрефиксовДобавляемыхРеквизитовФормы();

	Результат = Ложь;
	Для Каждого ТекПрефикс ИЗ ПрефиксыДобавляемыхРеквизитов Цикл
		Если СтрНачинаетсяС(ИмяРеквизита, ТекПрефикс) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
КонецФункции

&НаСервере
Процедура УдалитьДобавленныеРеквизитыФормы()
	МассивУдаляемыхРеквизитов = Новый Массив;
	Для Каждого Реквизит ИЗ ТаблицаРеквизитов Цикл
		Если Реквизит.ТабличнаяЧасть И ЗначениеЗаполнено(Реквизит.ТЧ) Тогда

			Продолжить;
		КонецЕсли;

		МассивУдаляемыхРеквизитов.Добавить(Реквизит.ИмяРеквизитаФормы);
	КонецЦикла;

	ИзменитьРеквизиты(, МассивУдаляемыхРеквизитов);

	ТаблицаРеквизитов.Очистить();
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитВТаблицуРеквизитов(Имя, ЭтоСтандартнтыйРеквизит,
		ИмяРеквизитаФормы, ТабличнаяЧасть = Ложь, ТЧ = "")
	НС = ТаблицаРеквизитов.Добавить();
	НС.Имя = Имя;
	НС.СтандартныйРеквизит = ЭтоСтандартнтыйРеквизит;
	НС.ИмяРеквизитаФормы = ИмяРеквизитаФормы;
	НС.ТабличнаяЧасть = ТабличнаяЧасть;
	НС.ТЧ = ТЧ;
КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыФормыОбъекта(МетаданныеОбъекта)
//	МетаданныеОбъекта = Справочники.Тестовый.ПустаяСсылка().Метаданные();
	МассивДобавляемыхРеквизитов = Новый Массив;

	Для Каждого ТекРеквизит ИЗ МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		ИмяРеквизита = "СтандартныйРеквизит_" + ТекРеквизит.Имя;

		Синоним = ТекРеквизит.Синоним;
		Если Не ЗначениеЗаполнено(Синоним) Тогда
			Синоним = ТекРеквизит.Имя;
		КонецЕсли;

		ТипРеквизита = ТекРеквизит.Тип;
		Если ВРег(ТекРеквизит.Имя) = "ССЫЛКА" Тогда
			ТипРеквизита = Новый ОписаниеТипов("Строка");
		КонецЕсли;
		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита, "", Синоним, Истина);
		МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

		ДобавитьРеквизитВТаблицуРеквизитов(ТекРеквизит.Имя, Истина, ИмяРеквизита);
	КонецЦикла;

	Для Каждого ТекРеквизит ИЗ МетаданныеОбъекта.Реквизиты Цикл
		ИмяРеквизита = "Реквизит_" + ТекРеквизит.Имя;

		Синоним = ТекРеквизит.Синоним;
		Если Не ЗначениеЗаполнено(Синоним) Тогда
			Синоним = ТекРеквизит.Имя;
		КонецЕсли;

		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, ТекРеквизит.Тип, "", Синоним, Истина);
		МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

		ДобавитьРеквизитВТаблицуРеквизитов(ТекРеквизит.Имя, Истина, ИмяРеквизита);
	КонецЦикла;

	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		ИмяРеквизита = "ТабличнаяЧасть_" + ТабличнаяЧасть.Имя;

		Синоним = ТабличнаяЧасть.Синоним;
		Если Не ЗначениеЗаполнено(Синоним) Тогда
			Синоним = ТабличнаяЧасть.Имя;
		КонецЕсли;

		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("ТаблицаЗначений"), "", Синоним, Истина);

		МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

		ДобавитьРеквизитВТаблицуРеквизитов(ТабличнаяЧасть.Имя, Ложь, ИмяРеквизита, Истина);

	КонецЦикла;

	ИзменитьРеквизиты(МассивДобавляемыхРеквизитов, );

	//Добавим реквизиты табличных частей
	МассивДобавляемыхРеквизитов = Новый Массив;
	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		ИмяРеквизитаТабличнойЧасти = "ТабличнаяЧасть_" + ТабличнаяЧасть.Имя;

		Для Каждого Реквизит ИЗ ТабличнаяЧасть.СтандартныеРеквизиты Цикл
			ИмяРеквизита = Реквизит.Имя;

			Синоним = Реквизит.Синоним;
			Если Не ЗначениеЗаполнено(Синоним) Тогда
				Если НРег(Реквизит.Имя) = "номерстроки" Тогда
					Синоним = "№";
				Иначе
					Синоним = Реквизит.Имя;
				КонецЕсли;
			КонецЕсли;

			НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Реквизит.Тип, ИмяРеквизитаТабличнойЧасти, Синоним, Истина);
			МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

			ДобавитьРеквизитВТаблицуРеквизитов(Реквизит.Имя, Истина, ИмяРеквизита, Истина, ТабличнаяЧасть.Имя);
		КонецЦикла;

		Для Каждого Реквизит ИЗ ТабличнаяЧасть.Реквизиты Цикл
			ИмяРеквизита = Реквизит.Имя;

			Синоним = Реквизит.Синоним;
			Если Не ЗначениеЗаполнено(Синоним) Тогда
				Синоним = Реквизит.Имя;
			КонецЕсли;

			НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Реквизит.Тип, ИмяРеквизитаТабличнойЧасти, Синоним, Истина);
			МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

			ДобавитьРеквизитВТаблицуРеквизитов(Реквизит.Имя, Ложь, ИмяРеквизита, Истина, ТабличнаяЧасть.Имя);
		КонецЦикла;
	КонецЦикла;

	ИзменитьРеквизиты(МассивДобавляемыхРеквизитов, );

КонецПроцедуры

&НаСервере
Процедура УдалитьДобавленныеЭлементыФормы()
	МассивГруппДляОчищения = Новый Массив;
	МассивГруппДляОчищения.Добавить(Элементы.ГруппаРеквизиты);
	МассивГруппДляОчищения.Добавить(Элементы.ГруппаСтраницыТабличныеЧасти);

	МассивЭлементовДляУдаления = Новый Массив;

	Для Каждого ТекГруппа ИЗ МассивГруппДляОчищения Цикл
		Для Каждого Элемент ИЗ ТекГруппа.ПодчиненныеЭлементы Цикл
			МассивЭлементовДляУдаления.Добавить(Элемент);
		КонецЦикла;
	КонецЦикла;

	Для Каждого Элемент Из МассивЭлементовДляУдаления Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыФормыДляРеквизитовОбъекта()
	СоответствиеТекущихРеквизитов = ТекущиеРеквизитыФормы();

	ГруппаРеквизитов = Элементы.ГруппаРеквизиты;

	Для Каждого СтрокаРеквизита Из ТаблицаРеквизитов Цикл
		Если СтрокаРеквизита.ТабличнаяЧасть Тогда
			Продолжить;
		КонецЕсли;

		РеквизитФормы = СоответствиеТекущихРеквизитов[СтрокаРеквизита.ИмяРеквизитаФормы];

		//		//Делаем группу под каждую константу, чтобы ее можно было разрисовывать
		//		ОписаниеГруппы = УИ_РаботаСФормами.НовыйОписаниеГруппыФормы();
		//		ОписаниеГруппы.Имя = "Группа_" + КлючЗначение.Ключ;
		//		ОписаниеГруппы.Заголовок = КлючЗначение.Значение.Заголовок;
		//		ОписаниеГруппы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		//		ОписаниеГруппы.ОтображатьЗаголовок = Ложь;
		//		ОписаниеГруппы.Родитель = ГруппаРеквизитов;
		//
		//		ГруппаТекущегоРеквизита= УИ_РаботаСФормами.СоздатьГруппуПоОписанию(ЭтотОбъект, ОписаниеГруппы);
		ОписаниеЭлемента = УИ_РаботаСФормами.НовыйОписаниеРеквизитаЭлемента();
		ОписаниеЭлемента.СоздаватьРеквизит = Ложь;
		ОписаниеЭлемента.СоздаватьЭлемент = Истина;
		ОписаниеЭлемента.Имя = СтрокаРеквизита.ИмяРеквизитаФормы;
		ОписаниеЭлемента.Заголовок = РеквизитФормы.Заголовок;
		ОписаниеЭлемента.ПутьКДанным = СтрокаРеквизита.ИмяРеквизитаФормы;
		ОписаниеЭлемента.Вставить("ПутьКРеквизиту", СтрокаРеквизита.ИмяРеквизитаФормы);
		ОписаниеЭлемента.РодительЭлемента = ГруппаРеквизитов;

		Если (РеквизитФормы.ТипЗначения.Типы().Количество() = 1
				И РеквизитФормы.ТипЗначения.СодержитТип(Тип("Булево"))) Тогда
			ОписаниеЭлемента.Параметры.Вставить("Вид", ВидПоляФормы.ПолеФлажка);
		КонецЕсли;

		Если ВРег(СтрокаРеквизита.Имя) = "ССЫЛКА" Тогда
			ОписаниеЭлемента.Параметры.Вставить("ТолькоПросмотр", Истина);
		КонецЕсли;

		//		Если ТекКонстанта.ЕстьХранилищеЗначения Тогда
		//			ОписаниеЭлемента.Параметры.Вставить("Доступность", Ложь);
		//		КонецЕсли;

		//ОписаниеЭлемента.Действия.Вставить("ПриИзменении", "КонстантаПриИзменении");
		УИ_РаботаСФормами.СоздатьЭлементПоОписанию(ЭтотОбъект, ОписаниеЭлемента);

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыДляТабличныхЧастейОбъекта()
	СоответствиеТекущихРеквизитов = ТекущиеРеквизитыФормы();

	//Табличные части
	СтруктураПоискаТабличныхЧастей = Новый Структура;
	СтруктураПоискаТабличныхЧастей.Вставить("ТабличнаяЧасть", Истина);
	СтруктураПоискаТабличныхЧастей.Вставить("ТЧ", "");

	ТабличныеЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоискаТабличныхЧастей);

	ГруппаТабличныхЧастей = Элементы.ГруппаСтраницыТабличныеЧасти;
	Для Каждого ТекТЧ ИЗ ТабличныеЧасти Цикл
		РеквизитФормыТЧ = СоответствиеТекущихРеквизитов[ТекТЧ.ИмяРеквизитаФормы];

		//Для каждой ТЧ добавляем свою закладку табличной части
		//Делаем группу под каждую константу, чтобы ее можно было разрисовывать
		ОписаниеГруппы = УИ_РаботаСФормами.НовыйОписаниеГруппыФормы();
		ОписаниеГруппы.Имя = "Группа_ТЧ_" + ТекТЧ.ИмяРеквизитаФормы;
		ОписаниеГруппы.Заголовок = РеквизитФормыТЧ.Заголовок;
		ОписаниеГруппы.Вид = ВидГруппыФормы.Страница;
		ОписаниеГруппы.ОтображатьЗаголовок = Истина;
		ОписаниеГруппы.Родитель = ГруппаТабличныхЧастей;

		ГруппаТекущегоРеквизита = УИ_РаботаСФормами.СоздатьГруппуПоОписанию(ЭтотОбъект, ОписаниеГруппы);

		ОписаниеЭлемента = УИ_РаботаСФормами.НовыйОписаниеРеквизитаЭлемента();
		ОписаниеЭлемента.СоздаватьРеквизит = Ложь;
		ОписаниеЭлемента.СоздаватьЭлемент = Истина;
		ОписаниеЭлемента.Имя = ТекТЧ.ИмяРеквизитаФормы;
		ОписаниеЭлемента.Заголовок = РеквизитФормыТЧ.Заголовок;
		ОписаниеЭлемента.ПутьКДанным = ТекТЧ.ИмяРеквизитаФормы;
		ОписаниеЭлемента.Вставить("ПутьКРеквизиту", ТекТЧ.ИмяРеквизитаФормы);
		ОписаниеЭлемента.РодительЭлемента = ГруппаТекущегоРеквизита;

		ОписаниеЭлемента.Параметры.Вставить("Тип", Тип("ТаблицаФормы"));
		ОписаниеЭлемента.Параметры.Вставить("ПутьКДанным", ТекТЧ.ИмяРеквизитаФормы);

		ОписаниеЭлемента.Действия.Вставить("ПриИзменении", "Подключаемый_ТабличнаяЧастьПриИзменении");
		ОписаниеЭлемента.Действия.Вставить("ПриНачалеРедактирования", "Подключаемый_ТабличнаяЧастьПриНачалеРедактирования");

		ТаблицаТЧ = УИ_РаботаСФормами.СоздатьЭлементПоОписанию(ЭтотОбъект, ОписаниеЭлемента);

		СтруктураПоискаТабличныхЧастей = Новый Структура;
		СтруктураПоискаТабличныхЧастей.Вставить("ТабличнаяЧасть", Истина);
		СтруктураПоискаТабличныхЧастей.Вставить("ТЧ", ТекТЧ.Имя);

		СоответствиеРеквизитовТЧ = ТекущиеРеквизитыФормы(ТекТЧ.ИмяРеквизитаФормы);

		РеквизитыТабличнойЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоискаТабличныхЧастей);
		Для Каждого РеквизитТЧ Из РеквизитыТабличнойЧасти Цикл
			РеквизитФормыРеквизитТЧ = СоответствиеРеквизитовТЧ[РеквизитТЧ.ИмяРеквизитаФормы];

			ОписаниеЭлемента = УИ_РаботаСФормами.НовыйОписаниеРеквизитаЭлемента();
			ОписаниеЭлемента.СоздаватьРеквизит = Ложь;
			ОписаниеЭлемента.СоздаватьЭлемент = Истина;
			ОписаниеЭлемента.Имя = РеквизитТЧ.ИмяРеквизитаФормы;
			ОписаниеЭлемента.Заголовок = РеквизитФормыРеквизитТЧ.Заголовок;
			ОписаниеЭлемента.ПутьКДанным = ТекТЧ.ИмяРеквизитаФормы + "."
				+ РеквизитТЧ.ИмяРеквизитаФормы;
			ОписаниеЭлемента.Вставить("ПутьКРеквизиту", ТекТЧ.ИмяРеквизитаФормы + "."
				+ РеквизитТЧ.ИмяРеквизитаФормы);
			ОписаниеЭлемента.РодительЭлемента = ТаблицаТЧ;

			ОписаниеЭлемента.Параметры.Вставить("Вид", ВидПоляФормы.ПолеВвода);
			Если НРег(РеквизитТЧ.Имя) = "номерстроки" Тогда
				ОписаниеЭлемента.Параметры.Вставить("ТолькоПросмотр", Истина);
			КонецЕсли;

			УИ_РаботаСФормами.СоздатьЭлементПоОписанию(ЭтотОбъект, ОписаниеЭлемента);

		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СоздатьОбновитьЭлементыФормыПоРеквизитамОбъекта(МетаданныеОбъекта)
	ДобавитьЭлементыФормыДляРеквизитовОбъекта();
	ДобавитьЭлементыДляТабличныхЧастейОбъекта();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеШапкиОбъектаНаФорме()
	Для Каждого Реквизит Из ТаблицаРеквизитов Цикл
		Если Реквизит.ТабличнаяЧасть Тогда
			Продолжить;
		КонецЕсли;

		Если ВРег(Реквизит.Имя) = "ССЫЛКА" Тогда
			ЭтотОбъект[Реквизит.ИмяРеквизитаФормы] = РедактируемыйОбъект[Реквизит.Имя].УникальныйИдентификатор();
		Иначе
			ЭтотОбъект[Реквизит.ИмяРеквизитаФормы] = РедактируемыйОбъект[Реквизит.Имя];
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТабличнойЧастиОбъектаНаФорме(ТекТабличнаяЧасть)
	ТЧОБъекта = РедактируемыйОбъект[ТекТабличнаяЧасть.Имя];

	ТаблицаФормы = ЭтотОбъект[ТекТабличнаяЧасть.ИмяРеквизитаФормы];
	ТаблицаФормы.Очистить();

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТабличнаяЧасть", Истина);
	СтруктураПоиска.Вставить("ТЧ", ТекТабличнаяЧасть.имя);

	РеквизитыТабличнойЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоиска);

	Для Каждого СтрокаТЧ ИЗ ТЧОБъекта Цикл
		НоваяСтрока = ТаблицаФормы.Добавить();

		Для Каждого РеквизитТЧ ИЗ РеквизитыТабличнойЧасти Цикл
			НоваяСтрока[РеквизитТЧ.ИмяРеквизитаФормы] = СтрокаТЧ[РеквизитТЧ.Имя];

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТабличныйЧастейОбъектаНаФорме()
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТабличнаяЧасть", Истина);
	СтруктураПоиска.Вставить("ТЧ", "");

	ТабличныеЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоиска);

	Для каждого ТекТабличнаяЧасть Из ТабличныеЧасти Цикл
		ЗаполнитьДанныеТабличнойЧастиОбъектаНаФорме(ТекТабличнаяЧасть);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОбъектаНаФорме()
	Если НЕ ЗначениеЗаполнено(РедактируемыйОбъект) Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьДанныеШапкиОбъектаНаФорме();
	ЗаполнитьДанныеТабличныйЧастейОбъектаНаФорме();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеОбъекта()
	Если НЕ ЗначениеЗаполнено(РедактируемыйОбъект) Тогда
		Возврат;
	КонецЕсли;

	МетаданныеОбъекта = РедактируемыйОбъект.Метаданные();

	ПолныйПутьКМетаданным = МетаданныеОбъекта.ПолноеИмя();

	Если ПолныйПутьКМетаданным <> ПолноеИмяОбъектаМетаданных Тогда
		УдалитьДобавленныеРеквизитыФормы();
		УдалитьДобавленныеЭлементыФормы();

		СоздатьРеквизитыФормыОбъекта(МетаданныеОбъекта);

		СоздатьОбновитьЭлементыФормыПоРеквизитамОбъекта(МетаданныеОбъекта);

		ПолноеИмяОбъектаМетаданных = ПолныйПутьКМетаданным;
	КонецЕсли;

	ЗаполнитьДанныеОбъектаНаФорме();
КонецПроцедуры
#КонецОбласти

#Область ЗаписьДанныхОбъекта

&НаСервере
Процедура ЗаполнитьРеквизитыНаСервереПередЗаписью(ОбъектЗаписи)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТабличнаяЧасть", Ложь);

	РеквизитыШапки = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоиска);

	МассивНеизменныхРеквизитов = Новый Массив;
	МассивНеизменныхРеквизитов.Добавить("ссылка");

	Для Каждого ТекРеквизит Из РеквизитыШапки Цикл
		Если МассивНеизменныхРеквизитов.Найти(НРег(ТекРеквизит.Имя)) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбъектЗаписи[ТекРеквизит.Имя] = ЭтотОбъект[ТекРеквизит.ИмяРеквизитаФормы];
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьНаСервереПередЗаписью(ОбъектЗаписи,
		ТекТабличнаяЧасть)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СтандартныйРеквизит", Ложь);
	СтруктураПоиска.Вставить("ТабличнаяЧасть", Истина);
	СтруктураПоиска.Вставить("ТЧ", ТекТабличнаяЧасть.имя);

	РеквизитыТабличнойЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоиска);

	ТЧОбъекта = ОбъектЗаписи[ТекТабличнаяЧасть.Имя];
	ТЧОбъекта.Очистить();

	ТаблицаФормы = ЭтотОбъект[ТекТабличнаяЧасть.ИмяРеквизитаФормы];

	Для Каждого СтрокаТаблицыФормы ИЗ ТаблицаФормы Цикл
		СтрокаТЧ = ТЧОбъекта.Добавить();

		Для Каждого ТекРеквизит Из РеквизитыТабличнойЧасти Цикл

			СтрокаТЧ[ТекРеквизит.Имя] = СтрокаТаблицыФормы[ТекРеквизит.ИмяРеквизитаФормы];
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныеЧастиНаСервереПередЗаписью(ОбъектЗаписи)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТабличнаяЧасть", Истина);
	СтруктураПоиска.Вставить("ТЧ", "");

	ТабличныеЧасти = ТаблицаРеквизитов.НайтиСтроки(СтруктураПоиска);

	Для каждого ТекТабличнаяЧасть Из ТабличныеЧасти Цикл
		ЗаполнитьТабличнуюЧастьНаСервереПередЗаписью(ОбъектЗаписи, ТекТабличнаяЧасть);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗаписьОбъектаВБазу(ОбъектЗаписи)
	ОбъектЗаписи.ОбменДанными.Загрузка = ЗаписьБезПроверок;

	Попытка
		ОбъектЗаписи.Записать();
		Модифицированность = Ложь;
	Исключение
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запись не удалась: "
			+ ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	Если НЕ ЗначениеЗаполнено(РедактируемыйОбъект) Тогда
		Возврат;
	КонецЕсли;

	ОбъектЗаписи = РедактируемыйОбъект.ПолучитьОбъект();

	ЗаполнитьРеквизитыНаСервереПередЗаписью(ОбъектЗаписи);
	ЗаполнитьТабличныеЧастиНаСервереПередЗаписью(ОбъектЗаписи);

	ВыполнитьЗаписьОбъектаВБазу(ОбъектЗаписи);

КонецПроцедуры

#КонецОбласти

#КонецОбласти



