#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтраницаРазработки = "http://infostart.ru/public/305892/";
	Элементы.СтраницаРазработки.Заголовок = "Инструкция";
	
КонецПроцедуры

#КонецОбласти


#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ТипОбъекта) Тогда
		ТипОбъектаПриИзмененииНаСервере();
	Иначе
		Объект.ИмяОбъекта = "";
		Объект.ПредопределенныеЭлементы.Очистить();
		Элементы.ИмяОбъекта.СписокВыбора.Очистить();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ТипОбъектаПриИзмененииНаСервере()
	
	СписокОбъектов = Новый СписокЗначений;
	
	Для каждого Элемент Из Метаданные[Объект.ТипОбъекта] Цикл
		
		СписокОбъектов.Добавить(Элемент.Имя,Элемент.Синоним);
		
	КонецЦикла; 
	
	Элементы.ИмяОбъекта.СписокВыбора.ЗагрузитьЗначения(СписокОбъектов.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяОбъектаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ИмяОбъекта) Тогда
		ИмяОбъектаПриИзмененииНаСервере();
	Иначе
		Объект.ПредопределенныеЭлементы.Очистить();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ИмяОбъектаПриИзмененииНаСервере()
	
	Если Объект.ТипОбъекта = "Справочники" Тогда
		ОбъектМетаданных = "Справочник";
	ИначеЕсли Объект.ТипОбъекта = "ПланыСчетов" Тогда
		ОбъектМетаданных = "ПланСчетов"; 	
	ИначеЕсли Объект.ТипОбъекта = "ПланыВидовХарактеристик" Тогда
		ОбъектМетаданных = "ПланВидовХарактеристик"; 
	ИначеЕсли Объект.ТипОбъекта = "ПланыВидовРасчета" Тогда
		ОбъектМетаданных = "ПланВидовРасчета"; 	
	Иначе
		Возврат;
	КонецЕсли; 
	Объект.ПолноеИмяОбъектаМетаданных = ОбъектМетаданных+"."+Объект.ИмяОбъекта;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектМетаданных.Ссылка КАК УстановленныйПредопределенныйЭлемент,
	|	ОбъектМетаданных.Ссылка КАК НовыйПредопределенныйЭлемент,
	|	ОбъектМетаданных.ИмяПредопределенныхДанных,
	|   Ложь КАК ЕстьОшибки
	|ИЗ
	|	ОбъектМетаданных.ИмяОбъектаМетаданных КАК ОбъектМетаданных
	|ГДЕ
	|	ОбъектМетаданных.Предопределенный";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"ОбъектМетаданных.ИмяОбъектаМетаданных",Объект.ПолноеИмяОбъектаМетаданных);
	
	Объект.ПредопределенныеЭлементы.Загрузить(Запрос.Выполнить().Выгрузить());
	Объект.ПредопределенныеЭлементы.Сортировать("ИмяПредопределенныхДанных возр");
	
	//Проверить, нет ли дублей
	ЕстьДубли = Ложь;
	Для ИндексСтроки = 1 По Объект.ПредопределенныеЭлементы.Количество()-1 Цикл
		Если Объект.ПредопределенныеЭлементы[ИндексСтроки-1].ИмяПредопределенныхДанных = Объект.ПредопределенныеЭлементы[ИндексСтроки].ИмяПредопределенныхДанных Тогда
			Объект.ПредопределенныеЭлементы[ИндексСтроки-1].ЕстьОшибки = Истина;
			Объект.ПредопределенныеЭлементы[ИндексСтроки].ЕстьОшибки = Истина;
			ЕстьДубли = Истина;		 
		КонецЕсли; 
	КонецЦикла; 
	Если ЕстьДубли Тогда
		Сообщить("Найдены элементы, которым сопоставлено несколько элементов ИБ!!! 
		|Удалите лишние сопоставления!");
	КонецЕсли; 
	
	//Добавить отсутствующие элементы
	ЕстьПропущенные = Ложь;
	МассивПредопределенных = Метаданные[Объект.ТипОбъекта][Объект.ИмяОбъекта].ПолучитьИменаПредопределенных();
	Для каждого Имя Из МассивПредопределенных Цикл
	    Если Объект.ПредопределенныеЭлементы.НайтиСтроки(Новый Структура("ИмяПредопределенныхДанных",Имя)).Количество() = 0 Тогда
			НоваяСтрока = Объект.ПредопределенныеЭлементы.Добавить();
			НоваяСтрока.ИмяПредопределенныхДанных = Имя;
			НоваяСтрока.УстановленныйПредопределенныйЭлемент = ПредопределенноеЗначение(Объект.ПолноеИмяОбъектаМетаданных+".ПустаяСсылка");
			НоваяСтрока.НовыйПредопределенныйЭлемент = ПредопределенноеЗначение(Объект.ПолноеИмяОбъектаМетаданных+".ПустаяСсылка");
			НоваяСтрока.ЕстьОшибки = Истина;
			ЕстьПропущенные = Истина;
		КонецЕсли;  
	КонецЦикла; 
	Если ЕстьПропущенные Тогда
		Сообщить("Найдены элементы, которым не сопоставлены элементы ИБ!!! 
		|Выполните сопоставления!");
	КонецЕсли; 
	
	Объект.ПредопределенныеЭлементы.Сортировать("ЕстьОшибки убыв, ИмяПредопределенныхДанных возр");
	
КонецПроцедуры


&НаКлиенте
Процедура СтраницаРазработкиНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(СтраницаРазработки);
КонецПроцедуры



#КонецОбласти

#Область СобытияТЧ

&НаКлиенте
Процедура ПредопределенныеЭлементыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ДобавляемаяСтрока = Элементы.ПредопределенныеЭлементы.ТекущиеДанные;
		ДобавляемаяСтрока.ИмяПредопределенныхДанных = "";
		ДобавляемаяСтрока.НовыйПредопределенныйЭлемент = ПредопределенноеЗначение(Объект.ПолноеИмяОбъектаМетаданных+".ПустаяСсылка");
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПредопределенныеЭлементыИмяПредопределенныхДанныхПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.ПредопределенныеЭлементы.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИмяПредопределенныхДанных) Тогда
		СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент = ПолучитьПредопределенныйЭлемент(Объект.ПолноеИмяОбъектаМетаданных,СтрокаТабличнойЧасти.ИмяПредопределенныхДанных);
		Если СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент = Неопределено Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Предопределенный элемент с имененем """+СтрокаТабличнойЧасти.ИмяПредопределенныхДанных+""" в конфигурации не определен";
			Сообщение.Поле = "Объект.ПредопределенныеЭлементы["+(СтрокаТабличнойЧасти.НомерСтроки-1)+"].ИмяПредопределенныхДанных";
			Сообщение.Сообщить();
			СтрокаТабличнойЧасти.ИмяПредопределенныхДанных = "";
			
		КонецЕсли; 
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область КомандыФормы

&НаКлиенте
Процедура ВыполнитьПереназначенияЭлементов(Команда)
	ВыполнитьПереназначенияЭлементовНаСервере();
	
	ОбновитьСписок(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ИмяОбъектаПриИзменении(Неопределено);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПереназначенияЭлементовНаСервере()
	Для каждого СтрокаТабличнойЧасти Из Объект.ПредопределенныеЭлементы Цикл
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИмяПредопределенныхДанных)
			И СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент <> СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент Тогда
			
			ТекстСообщения = " элемент ИБ изменен с """+СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент+""" на """+СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент+"""";
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент) Тогда
				
				// лишаем предопределённый элемент его предопределённости
				ОбновляемыйОбъект = СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент.ПолучитьОбъект();
				ОбновляемыйОбъект.ИмяПредопределенныхДанных = "";
				ОбновляемыйОбъект.ОбменДанными.Загрузка = Истина;
				//Снимем предопределенность строк
				Если Объект.ТипОбъекта = "Справочники" Тогда
					//Ничего не меняем, в 8.3.5 предопределенность строк не устанавливается
				ИначеЕсли Объект.ТипОбъекта = "ПланыСчетов" Тогда
					Для Каждого Субконто Из ОбновляемыйОбъект.ВидыСубконто Цикл
						Субконто.Предопределенное = Ложь;
					КонецЦикла;
				ИначеЕсли Объект.ТипОбъекта = "ПланыВидовХарактеристик" Тогда
					//Ничего не меняем, в 8.3.5 предопределенность строк не устанавливается
				ИначеЕсли Объект.ТипОбъекта = "ПланыВидовРасчета" Тогда
					Для каждого СтандартнаяТабличнаяЧасть Из Метаданные.ПланыВидовРасчета[Объект.ИмяОбъекта].СтандартныеТабличныеЧасти Цикл
						Для каждого ВидРасчета Из ОбновляемыйОбъект[СтандартнаяТабличнаяЧасть.Имя] Цикл
							ВидРасчета.Предопределенный = Ложь;
						КонецЦикла;
					КонецЦикла; 
				Иначе
					Возврат;
				КонецЕсли; 
				ОбновляемыйОбъект.Записать();     
				
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент) Тогда
					ТекстСообщения = " удалена связь с элементом ИБ """+СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент+"""";
				КонецЕсли; 
				
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент) Тогда
				
				// освободившуюся предопределённость отдаём новому элементу
				ОбновляемыйОбъект = СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент.ПолучитьОбъект();
				ОбновляемыйОбъект.ИмяПредопределенныхДанных = СтрокаТабличнойЧасти.ИмяПредопределенныхДанных;
				ОбновляемыйОбъект.ОбменДанными.Загрузка = Истина;
				ОбновляемыйОбъект.Записать();
				
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.УстановленныйПредопределенныйЭлемент) Тогда
					
					ТекстСообщения = " добавлена связь с элементом ИБ """+СтрокаТабличнойЧасти.НовыйПредопределенныйЭлемент+"""";
					
				КонецЕсли; 
				
				
			КонецЕсли; 
			
			Сообщить("Для предопределенного свойства """+СтрокаТабличнойЧасти.ИмяПредопределенныхДанных+""""+ ТекстСообщения);
		КонецЕсли; 
		
	КонецЦикла; 
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьПредопределенныйЭлемент(ПолноеИмяОбъектаМетаданных,ИмяПредопределенныхДанных)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектМетаданных.Ссылка КАК Ссылка,
	|	ОбъектМетаданных.Ссылка КАК НовыйПредопределенныйЭлемент,
	|	ОбъектМетаданных.ИмяПредопределенныхДанных
	|ИЗ
	|	ОбъектМетаданных.ИмяОбъектаМетаданных КАК ОбъектМетаданных
	|ГДЕ
	|	ОбъектМетаданных.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"ОбъектМетаданных.ИмяОбъектаМетаданных",ПолноеИмяОбъектаМетаданных);
	Запрос.УстановитьПараметр("ИмяПредопределенныхДанных",ИмяПредопределенныхДанных);
	
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
	Исключение
		Возврат Неопределено;
	КонецПопытки; 
	
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Ссылка;
		
	Иначе
		
		Возврат ПредопределенноеЗначение(ПолноеИмяОбъектаМетаданных+".ПустаяСсылка");
		
	КонецЕсли;  
	
КонецФункции // ПолучитьИмяПредопределенногоЭлемента()

&НаСервере
Процедура НайтиДублиНаСервере()
	
	СхемаЗапроса = Новый СхемаЗапроса;
	КоллекцияОператоры = СхемаЗапроса.ПакетЗапросов[0].Операторы;
	Для каждого ГруппаТаблиц Из СхемаЗапроса.ПакетЗапросов[0].ДоступныеТаблицы Цикл
		Если ГруппаТаблиц.Представление = "Справочники"
			ИЛИ ГруппаТаблиц.Представление = "ПланыСчетов"
			ИЛИ ГруппаТаблиц.Представление = "ПланыВидовРасчета"
			ИЛИ ГруппаТаблиц.Представление = "ПланыВидовХарактеристик" Тогда
			Для каждого Таблица Из ГруппаТаблиц.Состав Цикл
				ПоляТаблицы = Таблица.Поля;
				Для каждого ПолеТаблицы Из ПоляТаблицы Цикл
					Если ПолеТаблицы.Имя = "ИмяПредопределенныхДанных" Тогда
						НовыйОператор = КоллекцияОператоры.Добавить();
//						НовыйИсточник = НовыйОператор.Источники.Добавить(Таблица,"СправочникИмя");
						НовыйОператор.ВыбираемыеПоля.Добавить(""""+Таблица.Имя+"""");
						НовыйОператор.ВыбираемыеПоля.Добавить("КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СправочникИмя.ИмяПредопределенныхДанных)" );
						НовыйОператор.Группировка.Добавить("СправочникИмя.ИмяПредопределенныхДанных");
						НовыйОператор.Отбор.Добавить("СправочникИмя.Предопределенный");
						НовыйОператор.Отбор.Добавить("КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СправочникИмя.Ссылка) > 1");
						Продолжить;
					КонецЕсли; 
				КонецЦикла; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла; 
	СхемаЗапроса.ПакетЗапросов[0].Операторы.Удалить(0);
	СхемаЗапроса.ПакетЗапросов[0].Колонки[0].Псевдоним = "ИмяТаблицы";
	СхемаЗапроса.ПакетЗапросов[0].КонтрольныеТочкиИтогов.Добавить(СхемаЗапроса.ПакетЗапросов[0].Колонки[0]);
	СхемаЗапроса.ПакетЗапросов[0].ВыраженияИтогов.Добавить(СхемаЗапроса.ПакетЗапросов[0].Колонки[1]);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ТекстСообщения = "Некорректных данных не найдено!";	
	Иначе 
		ТекстСообщения = "Найдены предопределенные значения, которым соответствует несколько элементов ИБ:";
		ВыборкаИмяТаблицы = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИмяТаблицы.Следующий() Цикл
			ТекстСообщения = ТекстСообщения + Символы.ПС + ВыборкаИмяТаблицы.ИмяТаблицы+": "+ ВыборкаИмяТаблицы.ИмяПредопределенныхДанных;
		КонецЦикла; 
	КонецЕсли; 
	Сообщить(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиДубли(Команда)
	
	//СистемнаяИнформация = Новый СистемнаяИнформация;
	//Если СистемнаяИнформация.ВерсияПриложения < "8.3.5" Тогда
	//	ПоказатьПредупреждение(,"Проверка доступна для версии платформы не ниже 8.3.5");
	//	Возврат;
	//КонецЕсли; 
	
	НайтиДублиНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НайтиПропущенныеНаСервере()
	
	ДополнительныеПараметры = Новый Структура("СтрокаОшибок,НайденыПредопределенныеДанные","",Ложь);
	
	Для каждого Справочник Из Метаданные.Справочники Цикл
		ПроверитьПредопределенныеДанныеОбъекта(Справочник,ДополнительныеПараметры);
	КонецЦикла;
	Для каждого Справочник Из Метаданные.ПланыСчетов Цикл
		ПроверитьПредопределенныеДанныеОбъекта(Справочник,ДополнительныеПараметры);
	КонецЦикла;
	Для каждого Справочник Из Метаданные.ПланыВидовХарактеристик Цикл
		ПроверитьПредопределенныеДанныеОбъекта(Справочник,ДополнительныеПараметры);
	КонецЦикла;
	Для каждого Справочник Из Метаданные.ПланыВидовРасчета Цикл
		ПроверитьПредопределенныеДанныеОбъекта(Справочник,ДополнительныеПараметры);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.СтрокаОшибок) Тогда;
		Сообщить("Найдены предопределённые элементы, не сопоставленные с элементами ИБ:"+Символы.ПС+ДополнительныеПараметры.СтрокаОшибок);
	Иначе
		Сообщить("Все предопределённые данные сопоставлены с данными ИБ");
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьПредопределенныеДанныеОбъекта(ОбъекМетаданных,ДополнительныеПараметры)
	
	МассивПредопределенныхИмен = ОбъекМетаданных.ПолучитьИменаПредопределенных();
	Если МассивПредопределенныхИмен.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИмяОбъекта.ИмяПредопределенныхДанных
		|ИЗ
		|	"+ОбъекМетаданных.ПолноеИмя()+" КАК ИмяОбъекта
		|";
		ТаблицаПредопределнныхИБ = Запрос.Выполнить().Выгрузить();
		Для каждого ПредопределенноеИмя Из МассивПредопределенныхИмен Цикл
			Если ТаблицаПредопределнныхИБ.Найти(ПредопределенноеИмя,"ИмяПредопределенныхДанных") = Неопределено Тогда
				ДополнительныеПараметры.СтрокаОшибок = ДополнительныеПараметры.СтрокаОшибок + ОбъекМетаданных.ПолноеИмя()+"."+ПредопределенноеИмя+Символы.ПС;	
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;	
	
КонецПроцедуры // ПроверитьПредопределенныеДанныеОбъекта()


&НаКлиенте
Процедура НайтиПропущенные(Команда)
	НайтиПропущенныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПредопределенный(Команда)
	МассивВыделенныхСтрок = Новый Массив();
	Для каждого ЗначениеМассива из Элементы.ПредопределенныеЭлементы.ВыделенныеСтроки Цикл 
		МассивВыделенныхСтрок.Добавить(ЗначениеМассива);
	КонецЦикла;
	Для каждого ВыделеннаяСтрока Из МассивВыделенныхСтрок Цикл
		Элементы.ПредопределенныеЭлементы.ТекущаяСтрока = ВыделеннаяСтрока;
		Элементы.ПредопределенныеЭлементы.ТекущиеДанные.НовыйПредопределенныйЭлемент = Неопределено;
	КонецЦикла; 
КонецПроцедуры


#КонецОбласти 
