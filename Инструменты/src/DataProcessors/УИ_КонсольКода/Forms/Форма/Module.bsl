&НаКлиенте
Перем ЗакрытиеФормыПодтверждено;

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПолеАлгоритмаКлиент=УИ_РедакторКодаКлиентСервер.ТекстHTMLРедактораКода();
	ПолеАлгоритмаСервер=УИ_РедакторКодаКлиентСервер.ТекстHTMLРедактораКода();
КонецПроцедуры
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Не ЗакрытиеФормыПодтверждено Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ТекстАлгоритмаКлиентДокументСформирован(Элемент)
	Если ЗначениеЗаполнено(ТекстАлгоритмаКлиент) Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаКлиент", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстАлгоритмаСерверДокументСформирован(Элемент)
	Если ЗначениеЗаполнено(ТекстАлгоритмаСервер) Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаСервер", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СобытияКомандФормы
&НаКлиенте
Процедура ЗакрытьКонсоль(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗакрытьКонсольЗавершение", ЭтаФорма), "Выйти из консоли кода?",
		РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКод(Команда)
	//.1 Нужно обновить значения данных алгоритмов
	ОбновитьЗначениеПеременныхАлгоритмовИзРедактора();

	СтруктураПередачи = Неопределено;
	ВыполнитьАлгоритмНаКлиенте(СтруктураПередачи);
	ВыполнитьАлгоритмНаСервере(СтруктураПередачи);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Диалог.Заголовок = "Выберите имя файла для открытия";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "Файл алгоритма(*.xbsl)|*.xbsl|Все файлы(*.*)|*.*"; //Для обратной совместимости с *.alg1c
	Диалог.Расширение = "xbsl";

	НачатьПомещениеФайла(Новый ОписаниеОповещения("ОткрытьФайлЗавершение", ЭтаФорма), , Диалог, Истина,
		УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	СохранитьФайлНаДиск();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлКак(Команда)
	СохранитьФайлНаДиск(Истина);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗначениеКлиентскойПеременной(Команда)
	РедактироватьЗначениеПеременной(Элементы.ПеременныеКлиент);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗначениеСервернойПеременной(Команда)
	РедактироватьЗначениеПеременной(Элементы.ПеременныеСервер);
КонецПроцедуры

&НаКлиенте
Процедура НовыйАлгоритм(Команда)
	ИмяФайлаАлгоритма="";

	ТекстАлгоритмаКлиент="";
	ТекстАлгоритмаСервер="";

	УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаКлиент, ТекстАлгоритмаКлиент);
	УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаСервер, ТекстАлгоритмаСервер);

	УстановитьЗаголовок();
КонецПроцедуры

#КонецОбласти

#Область ПрочиеФункции

&НаКлиенте
Процедура СохранитьФайлНаДиск(СохранитьКак = Ложь)
	ОбновитьЗначениеПеременныхАлгоритмовИзРедактора();

	ПолучаемыеФайлы = Новый Массив;
	ОписаниеФайла = Новый ОписаниеПередаваемогоФайла;
	ОписаниеФайла.Хранение = ПоместитьВоВременноеХранилище(ПолучитьСтрокуСохранения(), УникальныйИдентификатор);

	ПолучаемыеФайлы.Добавить(ОписаниеФайла);

	Если Не СохранитьКак И ЗначениеЗаполнено(ИмяФайлаАлгоритма) Тогда
		НачатьПолучениеФайлов(Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтаФорма), ПолучаемыеФайлы, ИмяФайлаАлгоритма,
			Ложь);
	Иначе
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);

		Диалог.Заголовок = "Выберите имя файла для сохранения";
		Диалог.МножественныйВыбор = Ложь;
		Диалог.Фильтр = "Файл алгоритма(*.xbsl)|*.xbsl";
		Диалог.Расширение = "xbsl";

		НачатьПолучениеФайлов(Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтаФорма), ПолучаемыеФайлы, Диалог,
			Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКонсольЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытиеФормыПодтверждено = Истина;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаКлиент()
	Попытка
		УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаКлиент,
			ТекстАлгоритмаКлиент);
	Исключение
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаКлиент", 0.5, Истина);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаСервер()
	Попытка
		УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаСервер,
			ТекстАлгоритмаСервер);
	Исключение
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьТекстКодаВРедактореТекстаСервер", 0.5, Истина);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗначениеПеременныхАлгоритмовИзРедактора()
	ТекстАлгоритмаКлиент=УИ_РедакторКодаКлиентСервер.ТекстРедактораИзПоляРедактора(Элементы.ТекстАлгоритмаКлиент);
	ТекстАлгоритмаСервер=УИ_РедакторКодаКлиентСервер.ТекстРедактораИзПоляРедактора(Элементы.ТекстАлгоритмаСервер);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПодготовленныйКодАлгоритма(ТекстКода, Переменные)
	ПодготовленныйКод="";

	Для НомерПеременной = 0 По Переменные.Количество() - 1 Цикл
		ТекПеременная=Переменные[НомерПеременной];
		ПодготовленныйКод=ПодготовленныйКод + Символы.ПС + ТекПеременная.Имя + "=Переменные[" + Формат(НомерПеременной,
			"ЧН=0; ЧГ=0;") + "].Значение;";
	КонецЦикла;

	ПодготовленныйКод=ПодготовленныйКод + Символы.ПС + ТекстКода;

	Возврат ПодготовленныйКод;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВыполнитьАлгоритм(ТекстАлготима, Переменные, СтруктураПередачи)
	Успешно = Истина;
	ОписаниеОшибки = "";

	НачалоВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Попытка
		Выполнить (ТекстАлготима);
	Исключение
		Успешно = Ложь;
		ОписаниеОшибки = ОписаниеОшибки();
		Сообщить(ОписаниеОшибки);
	КонецПопытки;
	ОкончаниеВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах();

	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("Успешно", Успешно);
	РезультатВыполнения.Вставить("ВремяВыполнения", ОкончаниеВыполнения - НачалоВыполнения);
	РезультатВыполнения.Вставить("ОписаниеОшибки", ОписаниеОшибки);

	Возврат РезультатВыполнения;
КонецФункции

&НаКлиенте
Процедура ВыполнитьАлгоритмНаКлиенте(СтруктураПередачи)
	КодАлгоритма = ПодготовленныйКодАлгоритма(ТекстАлгоритмаКлиент, ПеременныеКлиент);

	Если Не ЗначениеЗаполнено(СокрЛП(КодАлгоритма)) Тогда
		Возврат;
	КонецЕсли;

	РезультатыВыполнения = ВыполнитьАлгоритм(КодАлгоритма, ПеременныеКлиент, СтруктураПередачи);

	Если РезультатыВыполнения.Успешно Тогда
		ЗаголовокЭлемента = "&&НаКлиенте (Время выполнения кода: " + Строка((РезультатыВыполнения.ВремяВыполнения)
			/ 1000) + " сек.)";
	Иначе
		ЗаголовокЭлемента = "&&НаКлиенте";
	КонецЕсли;
	Элементы.ГруппаКлиент.Заголовок = ЗаголовокЭлемента;

КонецПроцедуры

&НаСервере
Процедура ВыполнитьАлгоритмНаСервере(СтруктураПередачи)
	КодАлгоритма = ПодготовленныйКодАлгоритма(ТекстАлгоритмаСервер, ПеременныеСервер);

	Если Не ЗначениеЗаполнено(СокрЛП(КодАлгоритма)) Тогда
		Возврат;
	КонецЕсли;

	РезультатыВыполнения = ВыполнитьАлгоритм(КодАлгоритма, ПеременныеСервер, СтруктураПередачи);

	Если РезультатыВыполнения.Успешно Тогда
		ЗаголовокЭлемента = "&&НаСервере (Время выполнения кода: " + Строка((РезультатыВыполнения.ВремяВыполнения)
			/ 1000) + " сек.)";
	Иначе
		ЗаголовокЭлемента = "&&НаСервере";
	КонецЕсли;
	Элементы.ГруппаСервер.Заголовок = ЗаголовокЭлемента;

КонецПроцедуры

&НаСервере
Функция ПолучитьСтрокуСохранения()

	СохраняемыеДанные = Новый Структура;
	СохраняемыеДанные.Вставить("ТекстАлгоритмаКлиент", ТекстАлгоритмаКлиент);
	СохраняемыеДанные.Вставить("ТекстАлгоритмаСервер", ТекстАлгоритмаСервер);

	МассивПеременных=Новый Массив;
	Для Каждого ТекПеременная Из ПеременныеКлиент Цикл
		СтруктураПеременной=Новый Структура;
		СтруктураПеременной.Вставить("Имя", ТекПеременная.Имя);
		СтруктураПеременной.Вставить("Значение", ЗначениеВСтрокуВнутр(ТекПеременная.Значение));

		МассивПеременных.Добавить(СтруктураПеременной);
	КонецЦикла;
	СохраняемыеДанные.Вставить("ПеременныеКлиент", МассивПеременных);

	МассивПеременных=Новый Массив;
	Для Каждого ТекПеременная Из ПеременныеСервер Цикл
		СтруктураПеременной=Новый Структура;
		СтруктураПеременной.Вставить("Имя", ТекПеременная.Имя);
		СтруктураПеременной.Вставить("Значение", ЗначениеВСтрокуВнутр(ТекПеременная.Значение));

		МассивПеременных.Добавить(СтруктураПеременной);
	КонецЦикла;
	СохраняемыеДанные.Вставить("ПеременныеСервер", МассивПеременных);

	ЗаписьJSON=Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();

	ЗаписатьJSON(ЗаписьJSON, СохраняемыеДанные);

	Возврат ЗаписьJSON.Закрыть();

КонецФункции

&НаКлиенте
Процедура СохранитьФайлЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	Если ПолученныеФайлы=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолученныеФайлы.Количество()=0 Тогда
		Возврат;
	КонецЕсли;

	Модифицированность=Ложь;
	ИмяФайлаАлгоритма=ПолученныеФайлы[0].Имя;
	УстановитьЗаголовок();
	
//	Сообщить("Алгоритм успешно сохранен");

КонецПроцедуры

&НаСервере
Процедура ОткрытьАлгоритмНаСервере(АдресФайлаВоВременномХранилище)
	ДанныеФайла=ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);

	ЧтениеJSON=Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьПоток(ДанныеФайла.ОткрытьПотокДляЧтения());

	СтруктураФайла=ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();

	ТекстАлгоритмаКлиент=СтруктураФайла.ТекстАлгоритмаКлиент;
	ТекстАлгоритмаСервер=СтруктураФайла.ТекстАлгоритмаСервер;

	ПеременныеКлиент.Очистить();
	Для Каждого Переменная Из СтруктураФайла.ПеременныеКлиент Цикл
		НС=ПеременныеКлиент.Добавить();
		НС.Имя=Переменная.Имя;
		НС.Значение=ЗначениеИзСтрокиВнутр(Переменная.Значение);
	КонецЦикла;

	ПеременныеСервер.Очистить();
	Для Каждого Переменная Из СтруктураФайла.ПеременныеСервер Цикл
		НС=ПеременныеСервер.Добавить();
		НС.Имя=Переменная.Имя;
		НС.Значение=ЗначениеИзСтрокиВнутр(Переменная.Значение);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлЗавершение(Результат, Адрес, ПомещаемыйФайл, ДополнительныеПараметры) Экспорт

	Если Не Результат Тогда
		Возврат;
	КонецЕсли;

	Модифицированность=Ложь;
	ИмяФайлаАлгоритма=ПомещаемыйФайл;

	ОткрытьАлгоритмНаСервере(Адрес);

	УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаКлиент, ТекстАлгоритмаКлиент);
	УИ_РедакторКодаКлиентСервер.УстановитьТекстРедактораЭлемента(Элементы.ТекстАлгоритмаСервер, ТекстАлгоритмаСервер);
	
	УстановитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗначениеПеременной(ТаблицаФормы)
	ТекДанные=ТаблицаФормы.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	УИ_ОбщегоНазначенияКлиент.РедактироватьОбъект(ТекДанные.Значение);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовок()
	Заголовок=ИмяФайлаАлгоритма;
КонецПроцедуры
#КонецОбласти

ЗакрытиеФормыПодтверждено=Ложь;