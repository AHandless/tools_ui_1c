&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	Ответ=УИ_КоннекторHTTP.Get(URLАктуальногоРелиза);
	
	Если Ответ.КодСостояния>300 Тогда
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось скачать файл обновления с сервера");
		Возврат;
	КонецЕсли;

	ДвоичныеДанные=Ответ.Тело;
	
	Если ТипЗнч(ДвоичныеДанные)<>Тип("ДвоичныеДанные") Тогда
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Неправильный формат файла облновления");
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Имя", "УниверсальныеИнструменты");

	НайденныеРасширения = РасширенияКонфигурации.Получить(Отбор);

	Если НайденныеРасширения.Количество() = 0 Тогда
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не обнаружено расширение Универсальные инструменты");
		Возврат;
	КонецЕсли;

	НашеРасширение = НайденныеРасширения[0];
	
	// Проверим возможность применения расширения

	РезультатПроверки=НашеРасширение.ПроверитьВозможностьПрименения(ДвоичныеДанные,Ложь);

	Если РезультатПроверки.Количество()>0 Тогда
		Для Каждого ИнформацияОПроблемеПримененияРасширенияКонфигурации Из РезультатПроверки Цикл
			УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка применения расширения "+ИнформацияОПроблемеПримененияРасширенияКонфигурации.Описание);
		КонецЦикла;
			
		Возврат;
	КонецЕсли;

	Попытка
		НашеРасширение.Записать(ДвоичныеДанные);
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обновление успешно применено");
		
	Исключение
		УИ_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка применения обновления "+ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекущуюВерсию()
	Отбор = Новый Структура;
	Отбор.Вставить("Имя", "УниверсальныеИнструменты");

	НайденныеРасширения = РасширенияКонфигурации.Получить(Отбор);

	Если НайденныеРасширения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	НашеРасширение = НайденныеРасширения[0];
	ТекущаяВерсия = НашеРасширение.Версия;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАктуальнуюВерсиюИОписаниеИзменений()
//Получаем список всех релизов
	АдресЗапроса = "https://api.github.com/repos/cpr1c/tools_ui_1c/releases";

	МассивРелизов = УИ_КоннекторHTTP.GetJson(АдресЗапроса);

	МаксимальныйРелиз = "0.0.0";
	СоответствиеОписанияРелизов = Новый Соответствие;

	Для Каждого ТекРелиз Из МассивРелизов Цикл
		ВерсияТекРелиза = СтрЗаменить(ТекРелиз["tag_name"], "v", "");

		Если УИ_ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ВерсияТекРелиза, ТекущаяВерсия) > 0 Тогда
			СоответствиеОписанияРелизов.Вставить(ВерсияТекРелиза, ТекРелиз);
		КонецЕсли;

		Если УИ_ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ВерсияТекРелиза, МаксимальныйРелиз) <= 0 Тогда
			Продолжить;
		КонецЕсли;

		МаксимальныйРелиз = ВерсияТекРелиза;
		ВложенияРелиза = ТекРелиз["assets"];
		Если ВложенияРелиза = Неопределено Тогда
			URLАктуальногоРелиза = ""
		Иначе
			Для Каждого ТекВложение Из ВложенияРелиза Цикл
				ИмяФайлаРелиза = ТекВложение["name"];

				Если СтрНайти(НРег(ИмяФайлаРелиза),"cfe")=0 Тогда
					Продолжить;
				КонецЕсли;

				URLАктуальногоРелиза=ТекВложение["browser_download_url"];
				Прервать;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	АктуальнаяВерсия = МаксимальныйРелиз;

	ОписаниеИзменений = "";
	Для Каждого РелизОписания Из СоответствиеОписанияРелизов Цикл
		ОписаниеИзменений = ОписаниеИзменений + РелизОписания.Ключ + Символы.ПС;
		ОписаниеИзменений = ОписаниеИзменений + РелизОписания.Значение["body"]
			+ Символы.ПС;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьНеобходимостьОбновления()
	Если УИ_ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(АктуальнаяВерсия, ТекущаяВерсия) > 0 Тогда
		НеобходимостьОбновления = Истина;
	КонецЕсли;

	Элементы.ФормаОбновить.Видимость = НеобходимостьОбновления;
	Элементы.ОписаниеИзменений.Видимость = НеобходимостьОбновления;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьТекущуюВерсию();
	ЗаполнитьАктуальнуюВерсиюИОписаниеИзменений();
	УстановитьНеобходимостьОбновления();
КонецПроцедуры

